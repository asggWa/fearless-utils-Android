buildscript {
    ext {
        // App version
        versionName = '1.0.112'
        versionCode = 1

        // SDK and tools
        compileVersion = 33
        minVersion = 24
        targetVersion = 33

        kotlinVersion = '1.7.20'

        coroutinesVersion = '1.6.4'

        coroutinesDep = "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    }
    repositories {
        google()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'org.mozilla.rust-android-gradle:plugin:0.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven {
            url "https://jitpack.io"
        }
        mavenCentral()
    }
}

configurations {
    ktlint
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencies {
    ktlint("com.pinterest:ktlint:0.46.1") {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "--reporter=plain"
    args "--android"
    args "--reporter=checkstyle,output=${project.buildDir}/reports/checkstyle/ktlint.xml"
    args "$project.rootDir/**/src/main/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    mainClass.set("com.pinterest.ktlint.Main")
    args "-F", "$project.rootDir/**/src/main/**/*.kt"
    args "--android"
}
